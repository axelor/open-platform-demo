buildscript {
	apply from: "gradle/adk.gradle"
	dependencies {
		classpath adkPlugins()
	}
}

// find all modules
def modules = new File('modules').listFiles().findAll { dir ->
	new File(dir, 'build.gradle').exists() && !dir.name.endsWith('-tests')
}

apply plugin: "axelor-app"

application {

	name "axelor-demo"
	title "Axelor :: DEMO"

	version "4.2.0"

	// ADK version required
	adkVersion "~4.2.0"

	// depends on all modules
	modules.each { dir ->
		module "modules:$dir.name"
	}
}

allprojects {
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
}

tomcat {
	httpPort = project.properties.get("http.port", 8080) as Integer
	stopPort = 9451
}

// link adk projects in eclipse
allprojects {
	eclipse {
		adkLinks()
	}
}
